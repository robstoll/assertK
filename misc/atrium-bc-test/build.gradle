description = 'Checks that specs from older versions of Atrium can still be run with the components of the current version.'

//Enable if you need to check the task tree
//plugins {
//    id "com.dorongold.task-tree" version "1.3"
//}

repositories {
    maven { url "https://dl.bintray.com/jetbrains/spek" }
}

configurations {
    common
}

dependencies {
    common "org.junit.platform:junit-platform-gradle-plugin:$junit_platform_version"
    common("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    common("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    common("com.nhaarman:mockito-kotlin:$mockito_kotlin_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    common "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile configurations.common.dependencies
}

def createBcTasks(String apiName, String version, String forgive) {
    def nameUnderline = "${apiName.replaceAll('-', '_')}_${version.replaceAll("\\.", '_')}"
    def name = "${apiName}-$version"
    def srcName = "src_$nameUnderline"
    def confCommon = "conf_$nameUnderline"
    def testJar = "testJar_$nameUnderline"
    def testJarSources = "testJarSources_$nameUnderline"
    def srcDirectory = file("${project.projectDir}/src/old/$version/atrium-api-$apiName")

    sourceSets {
        create(srcName) {
            java.srcDirs = [] // we don't have java sources
            kotlin.srcDirs = [srcDirectory]
            resources.srcDirs = [] //we don't have resources
        }
    }

    configurations {
        create(confCommon)
        create(testJar)
        create(testJarSources)
    }

    dependencies {
        add(testJar, "ch.tutteli:atrium-api-$apiName:$version:tests") {
            exclude group: '*'
        }
        add(testJarSources, "ch.tutteli:atrium-api-$apiName:$version:testsources") {
            exclude group: '*'
        }

        add(confCommon, "ch.tutteli:atrium-spec:$version") {
            exclude group: '*'
        }
        add(confCommon, prefixedProject("api-cc-en_UK")) //required for atrium-spec
        add(confCommon, prefixedProject("$apiName-robstoll"))
        add(confCommon, sourceSets.main.runtimeClasspath)

        owner."${srcName}Compile" configurations[confCommon].dependencies
    }

    def getArgs = { String scanClassPath ->
        return [
            '--scan-class-path', scanClassPath,
            '--include-engine', 'spek-deprecation',
            '--config', forgive,
            '--details', 'summary'
        ]
    }

    def bbcTask = task("bbc-$name", type: JavaExec, group: 'Verification') {
        description = "Checks if specs from $apiName $version can be run against the current version."

        inputs.files configurations[testJar]
        inputs.files configurations[confCommon]

        classpath configurations[confCommon].asPath
        classpath configurations[testJar].asPath

        main = 'org.junit.platform.console.ConsoleLauncher'
        args = getArgs(configurations[testJar].asPath)
    }
    check.dependsOn bbcTask


    def unzip = task("unzip_$name") {
        inputs.files configurations[testJarSources]

        outputs.dir srcDirectory
        doLast {
            configurations[testJarSources].each {
                def jar = it
                copy {
                    from zipTree(jar)
                    into srcDirectory
                }
            }
        }
    }

    def bcTask = task("bc-$name", type: JavaExec, group: 'Verification') {
        description = "Checks if specs from $apiName $version can be compiled and run against the current version."

        inputs.files sourceSets[srcName].runtimeClasspath

        classpath sourceSets[srcName].runtimeClasspath

        main = 'org.junit.platform.console.ConsoleLauncher'
        args = getArgs(sourceSets[srcName].output.classesDirs.asPath)
    }

    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "$jacoco_tool_version"
        applyTo bcTask
        applyTo bbcTask
    }

    def jacocoReport = project.task("jacoco-bc-$name", type: JacocoReport) {
        sourceDirectories = sourceSets.main.allJava.sourceDirectories
        classDirectories = files(sourceSets.main.output.classesDirs)
        executionData bcTask
        executionData bbcTask

        sourceDirectories = project.sourceSets.main.allJava.sourceDirectories
        classDirectories = project.sourceSets.main.output.classesDirs

        def projects = apiName.contains('en_UK') ?
            jacocoMulti.sourceProjects :
            jacocoMulti.sourceProjects -
                prefixedProject('translations-en_UK') -
                prefixedProject('translations-en_UK-deprecated') +
                prefixedProject('translations-de_CH') +
                prefixedProject('translations-de_CH-deprecated')

        projects.each {
            sourceDirectories += it.sourceSets.main.allJava.sourceDirectories
            classDirectories += it.sourceSets.main.output.classesDirs
        }

        reports {
            csv.enabled = false
            xml.enabled = true
            xml.destination file("${buildDir}/reports/jacoco/bc-$name/report.xml")
            html.enabled = true
            html.destination file("${buildDir}/reports/jacoco/bc-$name/html/")
        }
    }

    check.dependsOn jacocoReport

    check.dependsOn bcTask
    project."compileSrc_${nameUnderline}Kotlin".dependsOn unzip
}

def createBcTasksForApis(String version, String forgive, String... apis) {

    def bc_version = task("bc-$version", group: 'Verification')
    def bbc_version = task("bbc-$version", group: 'Verification')

    apis.each { apiName ->
        createBcTasks(apiName, version, forgive)
        bc_version.dependsOn "bc-${apiName}-$version"
        bbc_version.dependsOn "bbc-${apiName}-$version"
    }
}

createBcTasksForApis('0.6.0',
    'forgive=.*spec:ch\\.tutteli\\.atrium\\.api\\.cc\\.(de_CH|en_UK|infix\\.en_UK)\\.Iterable.*(error cases|failing cases|context empty collection).*',
    'cc-de_CH', 'cc-en_UK', 'cc-infix-en_UK'
)
