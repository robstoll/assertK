import org.junit.platform.gradle.plugin.JUnitPlatformPlugin

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_platform_version"
    }
}
apply plugin: 'jacoco'
apply plugin: JUnitPlatformPlugin

repositories {
    maven { url "https://dl.bintray.com/jetbrains/spek" }
}

dependencies {
    testCompile("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}


junitPlatform {
    platformVersion "$junit_platform_version"
    filters {
        engines {
            include 'spek'
        }
    }
}

// adopted from http://stackoverflow.com/questions/39362955/gradle-jacoco-and-junit5
afterEvaluate {
    def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')
    def reportIndex = junitPlatformTestTask.args.findIndexOf { it == '--reports-dir' }
    def keep = (0..(junitPlatformTestTask.args.size() - 1)) - [reportIndex, reportIndex + 1]
    junitPlatformTestTask.args = junitPlatformTestTask.args[keep]

    jacoco {
        toolVersion = "$jacoco_tool_version"
        applyTo junitPlatformTestTask
    }

    def report = project.task('junitPlatformJacocoReport', type: JacocoReport) {
        sourceDirectories = sourceSets.main.allJava.sourceDirectories
        classDirectories = files(sourceSets.main.output.classesDirs)

        if (project.hasProperty('jacoco_additional')) {
            project.jacoco_additional.each {
                sourceDirectories += it.sourceSets.main.allJava.sourceDirectories
                classDirectories += it.sourceSets.main.output.classesDirs
            }
        }

        executionData junitPlatformTestTask
        reports {
            xml.enabled = true
            xml.destination file("${buildDir}/reports/jacoco/report.xml")
            html.enabled = true
            html.destination file("${buildDir}/reports/jacoco/html/")
            csv.enabled = false
        }
    }
    check.dependsOn report
}
